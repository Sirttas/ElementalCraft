buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'eclipse'
	id 'maven-publish'
	id "org.sonarqube" version "3.2.0"
	id "com.matthewprenger.cursegradle" version '1.4.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "elementalcraft.refmap.json"
}

version = project.hasProperty('tag_version') ? "${minecraft_version}-${tag_version}" : "${elementalcraft_version}"
group = 'sirttas.elementalcraft'
archivesBaseName = 'elementalcraft'

println('Building Elemental Craft: ' + project.version)

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
    }
    main {
    	resources {
    		srcDir 'src/generated/resources'
		}
		compileClasspath += api.output
        runtimeClasspath += api.output
	}
    datagen {
        java {
            srcDir 'src/datagen/java'
        }
    }
}

ext.updateSourceSetMapping = { sourceSet ->
	if (project.hasProperty('UPDATE_MAPPINGS')) {
		extractRangeMap.sources += sourceSet.java.srcDirs
		applyRangeMap.sources += sourceSet.java.srcDirs
		sourceSet.java.srcDirs.each { src -> 
			extractMappedNew.addTarget(src) 
		}
	}
}

configurations {
	apiCompile.extendsFrom(compile)
    apiImplementation.extendsFrom(implementation)
    apiCompileOnly.extendsFrom(compileOnly)
    datagenCompile.extendsFrom(compile)
    datagenImplementation.extendsFrom(implementation)
    datagenCompileOnly.extendsFrom(compileOnly)
}

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            property 'mixin.debug.verbose', 'true'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', createSrgToMcp.output

            mods {
                elementalcraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run-server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.debug.verbose', 'true'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', createSrgToMcp.output

            mods {
                elementalcraft {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', createSrgToMcp.output

            args '--mod', 'elementalcraft', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                elementalcraft {
                    source sourceSets.main
                    source sourceSets.datagen
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    mavenCentral()
    flatDir { dirs 'libs', '../DPAnvil/build/libs' }
    maven { url "https://maven.theillusivec4.top/" }
    maven { url "https://dvs1.progwml6.com/files/maven/" }
  	maven { url "https://modmaven.dev/" }
  	maven { url "https://maven.blamejared.com" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    
    annotationProcessor "org.spongepowered:mixin:${mixins_version}:processor"
    
    // mod dependencies
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    compileOnly fg.deobf("sirttas.dpanvil:DPAnvil:${dpanvil_version}:api")
    runtimeOnly fg.deobf("sirttas.dpanvil:DPAnvil:${dpanvil_version}")
    
    implementation fg.deobf("mezz.jei:jei-${jei_version}")
    
    // mod interactions
    implementation fg.deobf("mekanism:Mekanism:${mekanism_version}")
    
    // datagen
    datagenImplementation sourceSets.api.output
    datagenImplementation sourceSets.main.output
    datagenImplementation fg.deobf("mekanism:Mekanism:${mekanism_version}:tools")
}

jar {
	from sourceSets.api.output
    from sourceSets.main.output
    
    manifest {
        attributes([
            "Specification-Title": "ElementalCraft",
            "Specification-Vendor": "elementalcraft",
            "Specification-Version": "${version}",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"elementalcraft",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "elementalcraft.mixins.json"
        ])
    }
    afterEvaluate { finalizedBy('reobfJar') }
}

task apiJar(type: Jar) {
    archiveClassifier = "api"
    from sourceSets.api.output
    afterEvaluate { finalizedBy('reobfJar') }
}

task datagenJar(type: Jar) {
    archiveClassifier = "datagen"
    from sourceSets.datagen.output
    afterEvaluate { finalizedBy('reobfJar') }
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.api.allSource
    from sourceSets.main.allSource
    from sourceSets.datagen.allSource
}

build.dependsOn tasks.apiJar
build.dependsOn tasks.sourcesJar
build.dependsOn tasks.datagenJar

updateSourceSetMapping(sourceSets.api)
updateSourceSetMapping(sourceSets.datagen)

sonarqube {
	properties {
    	property "sonar.projectKey", "Sirttas_ElementalCraft"
    	property "sonar.organization", "sirttas"
    	property "sonar.host.url", "https://sonarcloud.io"
    	properties["sonar.sources"] += sourceSets.api.java.srcDirs
    	properties["sonar.sources"] += sourceSets.datagen.java.srcDirs
	}
}

if (System.getenv("MODMAVEN_USER") != null || project.hasProperty('modmaven_user')) {
	publishing {
	    publications {
	        mavenJava(MavenPublication) {
	        	artifactId = 'ElementalCraft'
				artifacts = [apiJar, jar, sourcesJar, datagenJar]
	        }
	    }
	    
	    repositories {
	        maven {
	            url = 'https://modmaven.dev/artifactory/local-releases/'
	            credentials {
					username = System.getenv("MODMAVEN_USER") ?: project.findProperty("modmaven_user")
	                password = System.getenv("MODMAVEN_PASSWORD") ?: project.findProperty("modmaven_password")
	            }
	        }
	    }
	}
}

if (System.getenv("CURSEFORGE_KEY") != null || project.hasProperty('curseforge_key')) {
	curseforge {
		apiKey = System.getenv("CURSEFORGE_KEY") ?: project.findProperty("curseforge_key")
	
		def changeLog = { ->
			def generatedChangelog = ""
			def changeStart = false;
			def changeEnd = false;
			
			println('Changelog:')
			
			def changelogFile = file('changelog.txt').withReader { reader ->
				def line
			
		        while ((line = reader.readLine()) != null && !changeEnd) {
		        	if (line ==~ '^\\s*- .*') {
		        		changeStart = true;
		        	} else if (changeStart) {
		        		changeEnd = true;
		        	}
					if (changeStart && !changeEnd) {
						println(line)
						generatedChangelog += line + '\n'
					}
				}
			}
			return generatedChangelog
		}
	
		project {
			id = '395158'
			changelog = changeLog
			releaseType = "${curse_release_type}"
			mainArtifact(jar) {
				relations {
					requiredDependency 'datapack-anvil'
					requiredDependency 'jei'
					requiredDependency 'patchouli'
	        		incompatible 'performant'
				}
			}
			addGameVersion "${minecraft_version}"
		}
	}
}
